program
    = { function_declaration }
    ;

declaration
    = variable_declaration
    | function_declaration
    ;

variable_declaration
    = "int" identifier [ "=" expression ] ";"
    ;

function_declaration
    = "int" identifier "(" param_list ")" ( block | ";" )
    ;

param_list
    = "void" | "int" identifier { "," "int" identifier }
    ;

block
    = "{" { block_item } "}"
    ;

block_item
    = statement
    | declaration
    ;

declaration
    = "int" identifier [ "=" expression ]
    ;

statement
    = "return" expression ";"
    | expression ";"
    | "if" "(" expression ")" statement [ "else" statement ]
    | block
    | "break" ";"
    | "continue" ";"
    | "while" "(" expression ")" statement
    | "do" statement "while" "(" expression ")" ";"
    | "for" "(" for_init [ expression ] ";" [ expression ] ")" statement
    | ";"
    ;

for_init
    = variable_declaration
    | [ expression ] ";"
    ;

expression
    = factor
    | expression binary_operator expression
    | expression "?" expression ":" expression
    ;

factor
    = integer_constant
    | identifier
    | unary_operator factor
    | "(" expression ")"
    | identifier "(" [ argument_list ] ")"
    ;

argument_list
    = expression { "," expression }
    ;

modifier_specifier
    = type_modifier
    | type_specifier
    | access_specifier
    ;

access_specifier
    = "external"
    | "internal"
    | "restrict"
    ;

type_modifier
    = "const"
    | "static"
    | "atomic"
    ;

type_specifier
    = "int"
    ;

assignment_operator
    = "="
    | "+="
    | "-="
    | "*="
    | "/="
    | "%="
    | "<<="
    | ">>="
    | "&="
    | "^="
    | "|="
    ;

binary_operator
    = "-"
    | "+"
    | "*"
    | "/"
    | "%"
    | "&&"
    | "||"
    | "=="
    | "!="
    | "<"
    | "<="
    | ">"
    | ">="
    | "="
    ;

unary_operator
    = "&"
    | "*"
    | "+"
    | "-"
    | "~"
    | "!"
    ;

identifier
    = ( letter | "_" ) { alphanumeric }
    ;

integer_constant
    = digit { digit }
    ;

alphanumeric
    = letter | digit
    ;

digit
    = "0".."9"
    ;

letter
    = "a".."z" | "A".."Z"
    ;
